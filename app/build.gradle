import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'multi-languages'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'armeabi', 'x86'
        }

        applicationId "org.cxct.sportlottery"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../signKey/key3.jks")
            storePassword "desheng@2020"
            keyAlias "fungame"
            keyPassword "desheng@2020"
        }
    }

    buildTypes {
        debug {
            buildConfigField 'Boolean', 'MOCK', 'false'//使用mock data要設為true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField 'Boolean', 'MOCK', 'false'//使用mock data要設為true
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    flavorDimensions "single"

    productFlavors {
        forqa {
            buildConfigField "String", "CHANNEL_NAME", "\"spplat1\""
            resValue "string", "app_name", "1bo sports"
            applicationId "com.happysport.sl.test"
            manifestPlaceholders = [
                    JPUSH_PKGNAME          : applicationId,
                    JPUSH_APPKEY           : "65670407e46038b18ee836ed", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL          : "developer-default", //暂时填写默认值即可.
                    NETWORK_SECURITY_CONFIG: "@xml/test_network_security_config"
            ]
        }
    }

    sourceSets {
        forqa.res.srcDirs = ['src/platform/test/res']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }

    // release apk時，不將.mock包入apk
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.mergeAssetsProvider.configure {
                doLast {
                    delete(fileTree(dir: outputDir, includes: ['**/*.mock']))
                }
            }
        }
    }

    //rename release .apk file
    applicationVariants.all { variant ->
        //判斷只有 release 版本才處理以下任務
        if (variant.buildType.name == 'release') {
            //改輸出名稱
            variant.outputs.all { output ->
                def name = variant.flavorName
                def date = new SimpleDateFormat("yyyyMMdd").format(new Date())
                outputFileName = name + '_' + getVersionCode() + '_' + getVersionName() + '_' + date + '.apk'
            }

            //強制改寫 apk 輸出路徑, ex: ..\app\build/outputs/releaseAPK
            //方便不同平台的 apk 可以在同一層級的資料夾整理
            variant.getPackageApplication().outputDirectory = new File(project.buildDir.absolutePath + "/outputs/releaseAPK")
        }
    }
}

ext {
    version_retrofit = "2.9.0"
    version_room = "2.2.6"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'

    //Activity Results API
    implementation 'androidx.activity:activity-ktx:1.2.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-rc01'

    //伸縮抽屜layout https://github.com/cachapa/ExpandableLayout
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'

    //導圓角的 ImageView
    implementation 'com.makeramen:roundedimageview:2.3.0'

    //Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$version_retrofit"
    implementation "com.squareup.okhttp3:okhttp:4.7.2"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.1'

    //ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    //navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'

    //multiLanguages //https://github.com/MichaelJokAr/MultiLanguages
    implementation "com.github.jokar:multi-languages:0.1.4"

    //live data
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'

    //annotation
    implementation 'androidx.annotation:annotation:1.1.0'

    //koin
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-android:$koin_version"

    //timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //calendar of date range picker
    implementation project(':awesome-calendar')

    //glide 處理圖片加載(支援.gif檔)
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    //WebSocket
//    implementation 'com.github.NaikSoftware:StompProtocolAndroid:1.6.6'
    implementation project(':stomp_protocol_android')

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    //相片選擇、拍照、錄影、剪裁 https://github.com/LuckSiege/PictureSelector
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.6.0'

    //動態權限請求 https://github.com/tbruyelle/RxPermissions
    implementation 'com.github.tbruyelle:rxpermissions:0.11'

    //圖片輪播功能 https://github.com/xiaohaibin/XBanner
    implementation 'com.xhb:xbanner:1.4.1'
    //androidX 版本使用下面的依赖
    implementation 'com.github.xiaohaibin:XBanner:androidx_v1.1.3'

    //極光推送sdk
    implementation 'cn.jiguang.sdk:jpush:3.9.1'
    implementation 'cn.jiguang.sdk:jcore:2.6.0'

    //room
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-ktx:$version_room"
    kapt "androidx.room:room-compiler:$version_room"

}

multiLanguages {
    enable = true
}